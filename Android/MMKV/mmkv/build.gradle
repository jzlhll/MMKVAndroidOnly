apply from: rootProject.file('gradle/build_library.gradle')

android {
    namespace "com.tencent.mmkv"
    //defaultPublishConfig "DefaultCppRelease"
//    defaultPublishConfig "StaticCppRelease"
//    defaultPublishConfig "SharedCppRelease"

    compileSdk 34
    defaultConfig {
        minSdk 23
        targetSdk 34

        externalNativeBuild {
            cmake {
                // uncomment this line to support 32-bit ABIs (armeabi-v7a & x86)
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
                // abiFilters 'arm64-v8a', 'x86_64'
            }
        }
        ndkVersion = '27.1.12297006'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    //解决打包失败问题
    lint {
        baseline = file("lint-baseline.xml")
        abortOnError false
        checkReleaseBuilds false
    }

    buildFeatures{
        aidl = true
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            externalNativeBuild {
                cmake {
                    cppFlags "-fvisibility=hidden", "-funwind-tables", "-fasynchronous-unwind-tables", "-O2"
                }
            }
            consumerProguardFiles 'proguard-rules.pro'
//            debuggable true
//            jniDebuggable true
        }
        debug {
            jniDebuggable true
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.18.0+"
        }
    }
    flavorDimensions  = ["stl_mode"]
    productFlavors {
        DefaultCpp {
            dimension "stl_mode"
            ext.artifactIdSuffix = ''
            externalNativeBuild {
                cmake {
                    arguments = ["-DANDROID_STL=c++_static", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"]
                    cppFlags "-DMMKV_STL_SHARED=0"
                }
            }
        }
        StaticCpp {
            dimension "stl_mode"
            ext.artifactIdSuffix = 'static'
            externalNativeBuild {
                cmake {
                    arguments = ["-DANDROID_STL=c++_static", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"]
                    cppFlags "-DMMKV_STL_SHARED=0"
                }
            }
        }
        SharedCpp {
            dimension "stl_mode"
            ext.artifactIdSuffix = 'shared'
            externalNativeBuild {
                cmake {
                    arguments = ["-DANDROID_STL=c++_shared", "-DANDROID_SUPPORT_FLEXIBLE_PAGE_SIZES=ON"]
                    cppFlags "-DMMKV_STL_SHARED=1"
                }
            }
        }
    }
}

configurations {
    javadocDeps
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.annotation:annotation:1.8.2'
    //javadocDeps 'androidx.annotation:annotation:1.8.2'
    compileOnly project(':mmkvannotation')
}
